import java.nio.file.Paths

plugins {
  id "java"
  id "idea"
  id "eclipse"
  id "com.moowork.node" version "1.2.0"
  id "org.springframework.boot" version "2.1.3.RELEASE"
  id "io.spring.dependency-management" version "1.0.7.RELEASE"
}

allprojects {
  group = "daggerok"
  version = "0.0.2"
  description = "Spring Boot 5 Reactive Angular ngrx"
}

sourceCompatibility = targetCompatibility = 1.8

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-thymeleaf")
  compile("org.springframework.boot:spring-boot-starter-webflux")
  runtime("org.springframework.boot:spring-boot-devtools")
  compileOnly("org.springframework.boot:spring-boot-configuration-processor")
  testCompileOnly("org.springframework.boot:spring-boot-configuration-processor")
  testCompile("org.springframework.boot:spring-boot-starter-test")
  testCompile("io.projectreactor:reactor-test")
}

wrapper {
  distributionType = "BIN"
  gradleVersion = "5.2.1"
}

node {
  download = true
  version = "10.9.0"
  npmVersion = "6.9.0"
  nodeModulesDir = Paths.get("src", "main", "js").toFile()
}

assemble.dependsOn npm_run_build
npm_run_build.dependsOn npm_install
npm_run_lint.dependsOn npm_install

clean.doFirst {
  ["nodejs", "npm", "yarn"].each { delete "$projectDir/.gradle/$it" }
  ["out", "build"].each { delete "$projectDir/$it" }
  delete Paths.get(node.nodeModulesDir.path, "node_modules")
}

defaultTasks "clean", "build"
